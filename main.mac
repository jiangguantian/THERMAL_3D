FINISH
/CLEAR
/FILNAME,BeiPanJiang_Archbridge
/TITLE,Thermal_Analysis
/PREP7
/NERR,,99999999


!-------------------START-----------------------!
!~~~~~~~~~~~~~读入TAX数组（综合大气温度以及综合换热系数）~~~~~~~~~~~~~~~~!
*DIM,TAX,ARRAY,14,6,,TIME,BUWEI
!*VREAD,TAX(1,1),E:/TAX_BPJ/TAX_BPJ,TXT,,JIK,6,14,,1
!(F6.3,5F8.3)
/INPUT,E:/TAX_BPJ/READ_TAX,TXT!包含了上面的两条语句，在GUI中必须用这种方式读入array
HT=TAX(1,2)    !顶板综合换热系数
HB=TAX(1,3)    !底板综合换热系数
HWD=TAX(1,4)    !东侧腹板综合换热系数，W/(O*K)
HWX=TAX(1,5)    !西侧腹板综合换热系数
HI=TAX(1,6)    !内部综合换热系数
*STATUS,TAX
!--------------------END-------------------------!



!~~~~~~~~~~~~~定义MAT,ETYPE及参数~~~~~~~~~~~~~~~~!
ET,2,SHELL131
KEYOPT,2,3,1
KEYOPT,2,4,10
!~~~~~~~~~~~~~C60钢筋混凝土
MP,EX,3,3.6E10
MP,PRXY,3,0.25
MP,DENS,3,2550
MP,KXX,3,3.4    !KXX-热传导系数，国际制单位是W/（m・K）
MP,KYY,3,3.4
MP,KZZ,3,3.4
MP,C,3,950    !比热容，J/(kg・K）

R1=0.5
R2=0.55
R3=0.6 
R4=0.65
R5=0.7 
R6=0.75
R7=0.8 
R8=0.9 
R9=1.1 

T1=R1/10
T2=R2/10
T3=R3/10
T4=R4/10
T5=R5/10
T6=R6/10
T7=R7/10
T8=R8/10
T9=R9/10
!~~~~~~~~~~~~~杆件分段数
MA=6
!----------------------END--------------------------!



!-------------------START-----------------------!
!~~~~~~~~~~~~~读入定义混凝土板的点号~~~~~~~~~~~~~~~~!
/INPUT,1-5KP1,mac
/INPUT,6-20KP1,mac
/INPUT,1-5KPB1,mac
/INPUT,6-7KPB1,mac
/INPUT,8-20KPB1,mac     
!----------------------END--------------------------!


!~~~~~~~~~~~~~~~~~~定义壳截面~~~~~~~~~~~~~~~~~~~~!
!----------------------START-------------------------!
!~~~~~~~~~~~~~~~~~~定义51~59号壳截面，对应R1~R9
!------定义51~59号壳截面
*DO,I,1,9,1

SECTYPE,50+I,SHELL
*DO,Z,1,10,1
SECDATA,T%I%,3,,3,LAYER%Z%!---------------输入第一层的厚度、材料号、层角、积分点数及层的名称
*ENDDO
SECOFFSET,MID
SECCONTROLS,0,0,0,0,1,1,1

*ENDDO
!-------------------------END-----------------------------!



!----------------------------START-----------------------------!
!~~~~~~~~~~~~~建立小里程左侧四分之一模型~~~~~~~~~~~~~~~~~~~~!
!~~~~~~~~~~~~~~~~~~建立1/4外包混凝土~~~~~~~~~~~~~~~~~~~~!
/INPUT,HNTB1-5-1,MAC
/INPUT,HNTB6-7-1,MAC
/INPUT,HNTB8-20-1,MAC
/INPUT,HLDB1,MAC


!~~~~~~~~~~~划分所有面单元
ASEL,S,TYPE,,2
AMESH,ALL
!~~~~~~~~~~~对称四分之一实体结构形成全桥结构~~~~~~~~~~~~~!
/INPUT,SYMM,MAC
CSDELE,ALL
!~~~~~~~~~~~融合所有重合节点
ALLSEL
NUMMRG,NODE


!~~~~~~~~~~~~~--------求解模块-----------~~~~~~~~~~~~~~~~~~~~!
!-------------------------START-----------------------------!
FINI
/SOLU
ANTYPE,TRANS
TRNOPT,FULL    !瞬态热分析唯一可以使用选项
TIMINT,ON    !瞬态热分析和稳态热分析唯一区别
TUNIF,29.5    !根据8点所有测点实测温度平均得出
OUTRES,ALL

!-----------------循环加载----------------------!
!------------------START--------------------!
!*DO,I,1,7,1
*DO,M,2,14,1
!M=8
TIME,3600*(M-1)
NSUBST,1,100,1
AUTOTS,ON
EQSLV,JCG
KBC,0    !采用渐进荷载


!-----------------施加对流荷载----------------------!
!------------------START--------------------!
!-------------给顶面施加对流荷载
ASEL,NONE

*DO,I,1,4,1
*DO,Z,1,20,1
ASEL,A,,,SB%I%%Z%
ASEL,A,,,ZSB%I%%Z%
*ENDDO
ASEL,A,,,HLDSB%I%
ASEL,A,,,HLDZSB%I%
*ENDDO

SFA,ALL,1,CONV,HT,TAX(M,2)
!-------------给底面施加对流荷载
ASEL,NONE

*DO,I,1,4,1
*DO,Z,1,20,1
ASEL,A,,,XB%I%%Z%
ASEL,A,,,ZXB%I%%Z%
*ENDDO
ASEL,A,,,HLDXB%I%
ASEL,A,,,HLDZXB%I%
*ENDDO

SFA,ALL,2,CONV,HB,TAX(M,3)
!-------------给WEBD（左侧面）施加对流荷载
ASEL,NONE

*DO,I,1,3,2
*DO,Z,1,20,1
ASEL,A,,,WB%I%%Z%
*ENDDO
ASEL,A,,,HLDWB%I%
*ENDDO

SFA,ALL,2,CONV,HWX,TAX(M,4)
!-------------给WEBX（右侧面）施加对流荷载
ASEL,NONE

*DO,I,2,4,2
*DO,Z,1,20,1
ASEL,A,,,WB%I%%Z%
*ENDDO
ASEL,A,,,HLDWB%I%
*ENDDO

SFA,ALL,2,CONV,HWD,TAX(M,5)
!-------------给箱子内部施加对流荷载
!-------------第一部分
ASEL,NONE

*DO,I,1,4,1
*DO,Z,1,20,1
ASEL,A,,,WB%I%%Z%
ASEL,A,,,NB%I%%Z%
ASEL,A,,,XB%I%%Z%
ASEL,A,,,ZXB%I%%Z%
*ENDDO
ASEL,A,,,HLDWB%I%
ASEL,A,,,HLDNB%I%
ASEL,A,,,HLDXB%I%
ASEL,A,,,HLDZXB%I%
*ENDDO

SFA,ALL,1,CONV,HI,TAX(M,6)
!-------------第二部分
ASEL,NONE

*DO,I,1,4,1
*DO,Z,1,20,1
ASEL,A,,,NB%I%%Z%
ASEL,A,,,SB%I%%Z%
ASEL,A,,,ZSB%I%%Z%
*ENDDO
ASEL,A,,,HLDNB%I%
ASEL,A,,,HLDSB%I%
ASEL,A,,,HLDZSB%I%
*ENDDO

SFA,ALL,2,CONV,HI,TAX(M,6)
!-----------------END---------------------!

ALLSEL,ALL
SOLVE
*ENDDO
!*ENDDO
!------------------------END---------------------------!


!!~~~~~~~~~~~~~~~~~~后处理模块~~~~~~~~~~~~~~~~~~~~!
!--------------------START-----------------------!
FINI
/POST1
SET,LAST
PLNSOL,TBOT

!!--------------------定义顶板温度变化路径
!NSEL,S,LOC,X,107.832,107.833
!NSEL,R,LOC,Z,80.000,80.001
!NSEL,R,LOC,Y,-8.4,8.4
!NUM=NDINQR(0,13)
!PATH,TOP-Z,NUM    !定义路径-顶板主方向
!N1=NODE(107.832,-8.4,80)
!PPATH,1,N1
!*DO,I,2,NUM
!N%I%=NNEAR(N%I-1%)
!NSEL,U,,,N%I-1%
!PPATH,I,N%I%
!*ENDDO
!ALLSEL
!PDEF,T-TOP-Z,TBOT    !定义路径上的计算温度值T
!PLPATH,T-TOP-Z
!PRPATH,T-TOP-Z
!!--------------------定义底板温度变化路径
!NSEL,S,LOC,X,111.050,111.051
!NSEL,R,LOC,Z,72.676,72.677
!NSEL,R,LOC,Y,-8.4,8.4
!NUM=NDINQR(0,13)
!PATH,BOT-Z,NUM    !定义路径-底板主方向
!N1=NODE(107.832,-8.4,80)
!PPATH,1,N1
!*DO,I,2,NUM
!N%I%=NNEAR(N%I-1%)
!NSEL,U,,,N%I-1%
!PPATH,I,N%I%
!*ENDDO
!ALLSEL
!PDEF,T-BOT-Z,TTOP    !定义路径上的计算温度值T
!PLPATH,T-BOT-Z
!PRPATH,T-BOT-Z
!!--------------------定义左侧腹板（WEBD）温度变化路径
!J=12000!------四分之一截面对应第12骨架节段，且左侧腹板对应12006/12007/12008三个点的连线
!PATH,WEDD-Z,2,,100    !定义路径-腹板主方向
!N1=NODE(107.832,8.4,80)
!N2=NODE(111.050,8.4,72.676)
!PPATH,1,N1
!PPATH,2,N2
!ALLSEL
!PDEF,T-WEDD-Z,TTOP    !定义路径上的计算温度值T
!PLPATH,T-WEDD-Z
!PRPATH,T-WEDD-Z
!!--------------------定义右侧腹板（WEBX）温度变化路径
!J=12000!------四分之一截面对应第12骨架节段，且左侧腹板对应12006/12007/12008三个点的连线
!PATH,WEDX-Z,2,,100    !定义路径-腹板主方向
!N1=NODE(107.832,-8.4,80)
!N2=NODE(111.050,-8.4,72.676)
!PPATH,1,N1
!PPATH,2,N2
!ALLSEL
!PDEF,T-WEDX-Z,TTOP    !定义路径上的计算温度值T
!PLPATH,T-WEDX-Z
!PRPATH,T-WEDX-Z



FINISH
/FILNAME,THERMAL_3D
/TITLE,Thermal_Analysis
/PREP7
ETCHG,TTS
KEYOPT,2,3,2
KEYOPT,2,8,1
MP,ALPX,2,1E-5!!混凝土的热膨胀系数是8~12xE-6/℃,决定于骨料种类、含量和混凝土配合比。
FINI
/SOLU
ANTYPE,STATIC

TREF,30
ALLSEL
SFADELE,ALL,1,CONV
ALLSEL
SFADELE,ALL,2,CONV
LDREAD,TEMP,6,,,,BeiPanJiang_Archbridge,RTH
ACEL,,,9.8

!----------------------START-----------------------!
!----------------定义主拱圈的约束-----------------!
KWPLAN,1,1002,1001,1003
CSYS,4
NSEL,S,LOC,Z,-0.01,0.01
NSEL,R,LOC,X,-0.1,28!后面发现有些交界墩的点也被约束故加上这个选择
NSEL,R,LOC,Y,-0.1,9!后面发现有些交界墩的点也被约束故加上这个选择
D,ALL,ALL
CSYS,0

A=-KX(1002)+445
B=KY(1002)
C=KZ(1002)
D=KP(A,B,C)

A=-KX(1001)+445
B=KY(1001)
C=KZ(1001)
E=KP(A,B,C)

A=-KX(1003)+445
B=KY(1003)
C=KZ(1003)
F=KP(A,B,C)

KWPLAN,1,D,E,F
CSYS,4
NSEL,S,LOC,Z,-0.01,0.01
NSEL,R,LOC,X,-0.1,28!后面发现有些交界墩的点也被约束故加上这个选择
NSEL,R,LOC,Y,-0.1,9!后面发现有些交界墩的点也被约束故加上这个选择
D,ALL,ALL
CSYS,0
!----------------------END-----------------------!



ALLSEL
SOLVE
FINI
/POST1
PLNSOL,S,EQV
FINI